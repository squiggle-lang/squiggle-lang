let (
    http = require("http"),
    keys = global.Object.keys,
    fromCharCode = global.String.fromCharCode,
    omap = fn(f, obj)
        map(fn(k) f(k, obj[k]), keys(obj)),
    NL = fromCharCode(10),
    between = fn(x) fn(a, b) a ++ x ++ b,
    concat = fn(a, b) a ++ b,
    handler = fn(req, res) do {
        console.log(req.method ++ " " ++ req.url);
        res.writeHead(200, {"Content-Type": "text/plain"});
        let (
            lines = omap(between(": "), req.headers),
            str = reduce(concat, map(fn(x) x ++ NL, lines))
        ) res.end(str);
    }
) do {
    http.createServer(handler).listen(1337, "127.0.0.1");
    print("Server started at http://127.0.0.1:1337/");
}
